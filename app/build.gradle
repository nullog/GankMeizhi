/*
 * Copyright 2015 XiNGRZ <chenxingyu92@gmail.com>
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "me.xingrz.gankmeizhi"

        minSdkVersion 19

        targetSdkVersion 23

        versionCode 150
        versionName "1.5"

        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "GitHub"]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'InvalidPackage', 'IconMissingDensityFolder'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        github {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "GitHub"]
        }

        fir {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "FIR"]
        }

        google {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "Google"]
        }

        wandoujia {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "Wandoujia"]
        }

        meizu {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "Meizu"]
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                output.outputFile = file("${output.outputFile.parent}/gkmz" +
                        "-${variant.buildType.name.toLowerCase()}" +
                        "-${variant.versionName}" +
                        "-${variant.flavorName.toLowerCase()}" +
                        ".apk")
            }
        }
    }
}

dependencies {
    compile 'com.android.support:design:24.0.0'
    compile 'com.android.support:cardview-v7:24.0.0'
    compile 'com.android.support:recyclerview-v7:24.0.0'

    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'

    compile 'com.jakewharton:butterknife:7.0.1'

    //compile 'com.squareup.okhttp:okhttp:2.7.4'
    compile 'com.squareup.okhttp3:okhttp:3.3.1'

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    // http://mvnrepository.com/artifact/com.squareup.retrofit2/converter-gson
    compile group: 'com.squareup.retrofit2', name: 'converter-gson', version: '2.1.0'

    compile 'com.google.code.gson:gson:2.7'

    compile 'com.github.bumptech.glide:glide:3.7.0'
    //compile 'com.github.bumptech.glide:okhttp-integration:1.4.0@aar'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'

    //compile 'io.realm:realm-android:0.82.2'

    compile 'com.umeng.analytics:analytics:latest.integration'
}
